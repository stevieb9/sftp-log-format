#!/usr/bin/perl

use warnings;
use strict;
use 5.12.0;

use Data::Dumper;

my $authlog = '/var/log/auth.log.0';
my $outfile = 'sftp-conn.log';

my $user_to_check = $ARGV[0];

open my $AUTHLOG, '<', $authlog 
    or die "Can't open the auth log file!: $!";

my %logdata;

while ( my $line = <$AUTHLOG> ) {
    
    chomp $line;

    if ( $line =~ /Invalid|Failed|error|keyboard-interactive|getaddrinfo/ ){
        next;
    }

    my ( $proc_id ) = $line =~ /\[(\d+)\]/;       
   
    if ( $line =~ /session opened for local user / ){
        next if $line !~ /$user_to_check/;
        ( $logdata{ $proc_id }{ time } ) = $line =~ /(.+\d+:\d+:\d+)/;
        ( $logdata{ $proc_id }{ user } ) = $line =~ /opened for local user (\w+) /;
        next;
    }

    my @entry = split (/\s+/, $line );
    my $time;
    for ( 1..3 ){
        $time .= " " . shift @entry;
    }
    
    if ( exists $logdata{ $proc_id } ) {
        my $stamped_entry = $time . " " . ( split( /: /, $line ) )[1];
        push @{ $logdata{ $proc_id }{ actions } }, $stamped_entry;
    }
}

close $authlog;

open my $OUTFILE, '+>', $outfile
    or die "Can't open the SFTP output log: $!";

for my $conn_id ( sort keys %logdata ) {

    print $OUTFILE 
        "\nUser: $logdata{ $conn_id }{ user }\nConnection: $conn_id" . 
        "\nConnection time: $logdata{ $conn_id }{ time }\nActions:\n";

    for my $action ( @{ $logdata{ $conn_id }{ actions } } ){

        print $OUTFILE "   $action\n";
    }
}


#print Dumper \%logdata;
